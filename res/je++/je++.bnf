statements

statements := statement statements
statements __EPSILON__

statement := print-statement
statement := block-statement
statement := return-statement
statement := expression-statement
statement := function-definition
statement := decl-statement
statement := if-statement
statement := if-else-statement
statement := loop
statement := loop-else
statement := loop-exit-statement
statement := nothing-statement

nothing-statement := nothing ;

if-else-statement := if-statement else-clause

if-statement := if ( expr ) statement
else-clause := else statement

loop-exit := break
loop-exit := continue
loop-exit-statement := loop-exit ;
loop-exit-statement := loop-exit INT-LITERAL ;

loop-else := loop else-clause

loop := while-loop
loop := until-loop
loop := do-while-loop
loop := do-until-loop
loop := for-loop
loop := foreach-loop

while-loop := while ( expr ) statement
until-loop := until ( expr ) statement
do-while-loop := do block-statement while ( expr ) ;
do-while-loop := do expr while ( expr ) ;
do-until-loop := do block-statement until ( expr ) ;
do-until-loop := do expr until ( expr ) ;

for-loop := for ( decl-statement expr ; expr ) statement
foreach-loop := foreach ( type IDENTIFIER : expr ) statement

expression-statement := expr ;
return-statement := return expr ;
block-statement := { statements }

print-statement := print expr ;
print-statement := println expr ;
print-statement := print STRING-LITERAL ;
print-statement := println STRING-LITERAL ;

decl-statement := type IDENTIFIER = assignment-expr ;

expr := assignment-expr

assignment-op := =
assignment-op := +=
assignment-op := -=
assignment-op := *=
assignment-op := /=
assignment-op := %=
assignment-expr := compare-expr
assignment-expr := IDENTIFIER assignment-op assignment-expr

compare-op := <=
compare-op := <
compare-op := >=
compare-op := >
compare-op := ==
compare-op := !=
compare-expr := add-expr
compare-expr := compare-expr compare-op add-expr

add-op := +
add-op := -
add-expr := mult-expr
add-expr := add-expr add-op mult-expr

mult-op := *
mult-op := /
mult-op := %
mult-expr := postfix-expr
mult-expr := mult-expr mult-op postfix-expr

postfix-expr := primary-expr
postfix-expr := postfix-expr postfix-op
postfix-expr := call-expr
postfix-expr := index-expr

postfix-op := --
postfix-op := ++

call-expr := postfix-expr ( )
call-expr := postfix-expr ( argument-list )
index-expr := postfix-expr [ argument-list ]
argument-list := expr
argument-list := expr , argument-list
argument-list := expr


primary-expr := INT-LITERAL
primary-expr := FLOAT-LITERAL
primary-expr := IDENTIFIER
primary-expr := ( expr )
primary-expr := input-expr

input-expr := input type

function-definition := func IDENTIFIER parameter-list -> return type expr ;
function-definition := func IDENTIFIER parameter-list -> type { statements }
parameter-list := ( )
parameter-list := ( parameters )
parameters := parameter
parameters := parameter , parameters
parameter := type IDENTIFIER
parameter := ref type IDENTIFIER
parameter := mut type IDENTIFIER

type := void
type := int
type := float